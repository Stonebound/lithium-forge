buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }

        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true

        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}-forge"
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()

    maven { url 'https://repo.spongepowered.org/maven' }
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                lithium {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                lithium {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'lithium', '--all', '--output', file('src/generated/resources/')

            mods {
                lithium {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "net.minecraftforge:forge:${project.forge_version}"

    shade "com.moandjiezana.toml:toml4j:0.7.2"
    compile "org.spongepowered:mixin:0.8.1-SNAPSHOT"
}

jar {
    manifest {
        attributes([
                "Specification-Title": "lithium",
                "Specification-Vendor": "JellySquid",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Implementation-Vendor" :"JellySquid",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLAT": "lithium_at.cfg",
                "MixinConfigs": "lithium.mixins.json"
        ])
    }
}

mixin {
    disableRefMapWarning = true
    add sourceSets.main, "mixins.lithium.refmap.json"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

shadowJar {
    relocate 'com.moandjiezana.toml', 'me.jellysquid.mods.lithium.shade.toml'
    configurations = [ project.configurations.shade ]
    classifier = 'mod'

    dependencies {
        exclude(dependency('com.google.code.gson:gson:.*'))
    }
}

jar.finalizedBy shadowJar


reobf {
    shadowJar {}
}